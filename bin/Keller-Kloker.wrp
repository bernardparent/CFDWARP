{
  _______________________________________________________________________________________________

                                           C F D W A R P

                                CFD Code for Plasma & Reactive Flow
  _______________________________________________________________________________________________

  Version                          22jul04a
  Compiler                         gcc 8.3.0 with OPENMP threads
  Compiled on                      Jul  8 2022 at 15:12:39
  Cycle Strategy                   Standard
  Fluid Relaxation                 Block ADI [1,2]
  EM Field Relaxation              None
  Convection Discretization        Reconstruction-Evolution MUSCL with flux FVS [3], FDS [5],
                                   and primitive interpolation TVD [4], WENO [7], CWENO [9],
                                   AOWENO [8], and eigenvalue conditioning HARTEN, GNOFFO [10],
                                   PECLET [11], PASCAL [6], and PARENT positivity-preserving
                                   filter [12]
  Temporal Discretization          Trapezoidal 2nd-Order
  Fluid Model                      Navier-Stokes Perfect Gas
  Thermodynamic Model              Perfect Gas
  Chemical Model                   Air 1 species 0 reaction
  EM Field Model                   None
  Beam Model                       None
  Number of Dimensions             3
  Number of Flux Components        5
  _______________________________________________________________________________________________

  [1]  WR Briley and H McDonald. "On the Structure and Use of Linearized Block Implicit
       Schemes", Journal of Computational Physics 34:54-73, 1980.
  [2]  RM Beam and RF Warming. "An Implicit Finite-Difference Algorithm for Hyperbolic Systems
       in Conservation-Law-Form", Journal of Computational Physics 22:87-110, 1976.
  [3]  JL Steger and RF Warming. "Flux Vector Splitting of the Inviscid Gasdynamic Equations
       with Application to Finite-Difference Methods", Journal of Computational Physics 40:263-
       293, 1981.
  [4]  WK Anderson, JL Thomas, and B Van Leer. "Comparison of Finite Volume Flux Vector
       Splittings for the Euler Equations", AIAA Journal 24:1453-1460, 1986.
  [5]  PL Roe. "Approximate Riemann Solvers, Parameter Vectors, and Difference Schemes", Journal
       of Computational Physics 43:357-372, 1981.
  [6]  B Parent. "Positivity-Preserving Dual Time Stepping Schemes for Gas Dynamics", Journal of
       Computational Physics 361:391-411, 2018.
  [7]  G Jiang and CW Shu. "Efficient Implementation of Weighted ENO Schemes", Journal of
       Computational Physics 126:202-228, 1996.
  [8]  DS Balsara, S Garain, and CW Shu. "An Efficient Class of WENO schemes with Adaptive
       Order", Journal of Computational Physics 326:780-804, 2016.
  [9]  M Dumbser and M Kaser. "Arbitrary High Order Non-oscillatory Finite Volume Schemes on
       Unstructured Meshes for Linear Hyperbolic Systems", Journal of Computational Physics,
       221:693-723, 2007.
  [10] P Gnoffo and  JA White. "Computational Aerothermodynamic Simulation Issues on
       Unstructured Grids", 37th AIAA Thermophysics Conference, AIAA Paper 2004-2371, 2004.
  [11] B Parent. "Multidimensional High-Resolution Schemes for Viscous Hypersonic Flows", AIAA
       Journal, 55:141-152, 2017.
  [12] B Parent. "Making a Flux Positivity-Preserving: A General Purpose Filter for the Euler
       Equations", AIAA Paper 2019-0906, AIAA Scitech, San Diego CA, 2019.


}

 
{
LES of a turbulent supersonic boundary layers

See: DNS of Effusion Cooling in a Supersonic Boundary-Layer Flow: Influence of Turbulence by Michael Keller and Markus J. Kloker, AIAA 2013-2897 
and DNS of a flat-plate supersonic boundary layer using the discontinuous Galerkin spectral element method by M. Atak, J. Larsson,  G. Gassner, C.-D. Munz, AIAA 2014-3078

}

freq=50000;{Hz}
poweradded=25; {W}

Minf=2.67;
Tinf=564; {K}
Pinf=14890; {Pa}
Twall=1242; {K}
Lref=24.77E-3; {m}
Lx=10.4*Lref;
Ly=0.93*Lref;
Lz=2*pi/21*Lref;
deltalam=0.025*Lref;
deltaturb=0.175*Lref;

mf=1;
numtimelevelsskip_writedatafile=round(6*mf);

is=1;
ie=round(925*mf);
js=1;
je=round(60*mf);
ks=1;
ke=round(40*mf);

TURB_js=js;
TURB_je=round(40*mf);
TURB_is=round(225*mf);
TURB_ie=ie;
TURB_ys=0;
TURB_ye=deltaturb;
TURB_xs=0.25*Lx;
TURB_xe=Lx;
TURB_dwall=20e-6/mf;

HEAT_is=round(30*mf);
HEAT_ie=round(100*mf);
HEAT_js=js;
HEAT_je=round(10*mf);
HEAT_ks=ks+round(10*mf);
HEAT_ke=ke-round(10*mf);
HEAT_xs=0.05*Lx;
HEAT_xe=0.12*Lx;
HEAT_ys=0.0;
HEAT_ye=deltalam/4;
HEAT_zs=Lz*0.2;
HEAT_ze=Lz*0.7;

Grid(
  Size(is,js,ks, ie,je,ke);

  {heat region}
  Corners    (HEAT_is,HEAT_js,HEAT_ks, HEAT_ie,HEAT_je,HEAT_ke, HEAT_xs,HEAT_ys,HEAT_zs,HEAT_xe,HEAT_ye,HEAT_ze);
  JoinCorners(HEAT_is,HEAT_js,HEAT_ks, HEAT_ie,HEAT_je,HEAT_ke,  EE,0.5e0,1.0e0,1.0e0,  EE,0.5e0,1.0e0,1.0e0,  EE,0.5e0,1.0e0,1.0e0);

  {between heat region and bdry height}
  Corners    (HEAT_is,HEAT_je,HEAT_ks,HEAT_ie,TURB_je,HEAT_ke, HEAT_xs,HEAT_ye,HEAT_zs,HEAT_xe,TURB_ye,HEAT_ze);
  JoinCorners(HEAT_is,HEAT_je,HEAT_ks,HEAT_ie,TURB_je,HEAT_ke,  EE,0.5e0,1.0e0,1.0e0,  GE,0.5e0,1.0e0,1.0e0,  EE,0.5e0,1.0e0,1.0e0);

  {between bdry height and top of domain}
  Corners    (HEAT_is,TURB_je,HEAT_ks, HEAT_ie,je,HEAT_ke, HEAT_xs,TURB_ye,HEAT_zs,HEAT_xe,Ly,HEAT_ze);
  JoinCorners(HEAT_is,TURB_je,HEAT_ks, HEAT_ie,je,HEAT_ke,  EE,0.5e0,1.0e0,1.0e0,  GE,0.8e0,1.0e0,1.0e0,  EE,0.5e0,1.0e0,1.0e0);

  {Region between HEAT_ke and ke}  
  Copy(HEAT_is,js,HEAT_ke, HEAT_ie,je,HEAT_ke, HEAT_is,js,ke);
  Translate(HEAT_is,js,ke,HEAT_ie,je,ke, 0,0,Lz-HEAT_ze);
  Join(HEAT_is,js,HEAT_ke,HEAT_ie,je,ke,k,GE,0.5,1.0,1.0);  

  {Region between ks and HEAT_ks}  
  Copy(HEAT_is,js,HEAT_ks, HEAT_ie,je,HEAT_ks, HEAT_is,js,ks);
  Translate(HEAT_is,js,ks,HEAT_ie,je,ks, 0,0,-HEAT_zs);
  Join(HEAT_is,js,ks,HEAT_ie,je,HEAT_ks,k,EG,0.5,1.0,1.0);  


  {Region between is and HEAT_is}
  Copy(HEAT_is,js,ks,HEAT_is,je,ke, is,js,ks);
  Translate(is,js,ks,is,je,ke,-HEAT_xs,0,0);
  Join(is,js,ks,HEAT_is,je,ke,i,EG,0.5,1,1);

  {Region between TURB_is and TURB_ie}
  Corners(TURB_is,TURB_js,ks,TURB_ie,TURB_je,ke, TURB_xs,TURB_ys,0,TURB_xe,TURB_ye,Lz);
  JoinCorners(TURB_is,TURB_js,ks,TURB_ie,TURB_je,ke,  EE,0.5,1,1, FE,0.4,TURB_dwall,1, EE,0.5,1,1);
  Corners(TURB_is,TURB_je,ks,TURB_ie,je,ke, TURB_xs,TURB_ye,0,TURB_xe,Ly,Lz);
  JoinCorners(TURB_is,TURB_je,ks,TURB_ie,je,ke,  EE,0.5,1,1, GE,0.8,1,1, EE,0.5,1,1);

  {Region between HEAT_ie and TURB_is}
  Join(HEAT_ie,js,ks, TURB_is,je,ke, i, GG, 0.5, 1,1);

{
  printf("\ndx in turb region =  %E m\n",_x(TURB_is+1,js,ks)-_x(TURB_is,js,ks));
  printf("dy in turb region =  %E m\n",_y(TURB_is+1,TURB_je,ks)-_y(TURB_is+1,TURB_je-1,ks));
  printf("dz in turb region =  %E m\n",_z(TURB_is+1,TURB_je,ks+1)-_z(TURB_is+1,TURB_je,ks));
}
);



Block(
  Fluid(    
    for (i,is+1,ie-1,
      for (j,js+1,je-1,
        Link(i,j,ks+1,i,j,ke);
        Link(i,j,ks,i,j,ke-1);
      );
    );
  );
);


Bdry(
  NavierStokesPerfect(
    {
    _________________________________________________________________________________________

    Boundary Condition Type    ID    Description
    _________________________________________________________________________________________

    BDRY_INFLOWSUPERSONIC      0     Inflow, supersonic
    BDRY_OUTFLOWSUPERSONIC1    1     Outflow, supersonic, 1o
    BDRY_SYMMETRICAL1          2     Symmetrical, 1o
    BDRY_SYMMETRICAL2          3     Symmetrical, 2o
    BDRY_SYMMETRICAL3          8     Symmetrical, 3o
    BDRY_WALLTFIXED1           4     Wall, T specified, 1o
    BDRY_WALLTFIXED2           a     Wall, T specified, 2o
    BDRY_WALLADIABATIC1        5     Wall, adiabatic, 1o
    BDRY_WALLADIABATIC2        b     Wall, adiabatic, 2o
    BDRY_INFLOWSUBSONIC1       6     Subsonic Inflow 1o (Constant Tstag, Pstag at inflow)
    BDRY_OUTFLOWSUBSONIC1      7     Subsonic Outflow 1o (Constant P at outflow)
    BDRY_FREESTREAM1           9     Freestream, 1o, params Vx,Vy,Vz, P, T
    _________________________________________________________________________________________
    }
    All(BDRY_OUTFLOWSUPERSONIC1);
    
    Plane("i",is,BDRY_INFLOWSUPERSONIC);
    Plane("i",ie,BDRY_OUTFLOWSUPERSONIC1);
    Plane("j",js,BDRY_WALLTFIXED1,Twall);
    Plane("j",je,BDRY_OUTFLOWSUPERSONIC1);
    Plane("k",ks,BDRY_SYMMETRICAL2);
    Plane("k",ke,BDRY_SYMMETRICAL2);
    
  );
);
 
 
Model(
  NavierStokesPerfect(
    gamma=1.4;
    R=286.0;        {J/kgK }
    ETAMODEL=ETAMODEL_CONSTANT;     
    eta=2.8e-5;       {kg/ms}
    Pr=0.71;     
    Pmin=1.0e-2;    Pmax=9.9e99;   {Pa}
    Tmin=1.0e1;     Tmax=6.0e3;    {K}
    SetBodyForce(is,js,ks, ie,je,ke, 0.0{N/m3},0.0{N/m3},0.0{N/m3});
    SetHeatDeposited(is,js,ks, ie,je,ke, 0.0 {W/m3});
    {
    AddHeatPoint(0.0{x,m},0.0{y,m},0.0{z,m}, 0.1{radius,m}, 0.0{W});
    }
  );
);


Init(
  NavierStokesPerfect(
    {
    _______________________________________________________________________________________

    Initial Condition Type       Parameters
    _______________________________________________________________________________________

    INIT_TYPE1                   V[1]..V[nd],  P,  T     
    INIT_TYPE2                   M[1]..M[nd],  P,  T     
    INIT_TYPE3                   V[1]..V[nd],  P,  rho   
    _______________________________________________________________________________________
    }
    Mx=Minf;
    My=0;
    Mz=0;
    P=Pinf; {Pa}
    T=Tinf; {K}
    
    vel_profile[1]=0;
    den_profile[1]=1E-6;{doesn't take effect: will be replaced by value at wall}
	flag=0;
    for(j,js+1,je,
	  y=_y(1,j,1);
	  if(flag==0,
	    vel_profile[j]=(3.80507337626901E+15*y^5-1.61299239768002E+12*y^4+1.14565790143603E+08*y^3+7.37488485403774E+05*y^2+1.54657986330626E+00
*y)/(3.12094964218140E+12*y^5-1.79357558919418E+09*y^4+1.01732458800873E+06*y^3-3.20126031135779E+02*y^2+4.28937718377614E-01*y);
		profileused=j;
		if(vel_profile[j]<vel_profile[j-1] || y>0.001771669718,
          flag=flag+1;
          vel_profile[j]=vel_profile[j-1];
		  profileused_v=j-1;
        );
      ,{else}
        vel_profile[j]=vel_profile[profileused_v];
      );
    );


	flag=0;
    for(j,js+1,je,
	  y=_y(1,j,1);
	  if(flag==0,
	    den_profile[j]=(494594121395.75*y^5+508676499.243329*y^4-1134734.94671323*y^3+1060.15479630791*y^2-0.288269060313963*y)/(4271751239518.71*y^5+12507040376.2409*y^4-28585405.390141*y^3+26199.8863484573*y^2-6.82710958171928*y);
		profileused=j;
		if(den_profile[j]<den_profile[j-1] || y>0.002910816576,
          flag=flag+1;
          den_profile[j]=den_profile[j-1];
		  profileused=j-1;
        );
      ,{else}
        den_profile[j]=den_profile[profileused];
      );
	  printf("y=%lf,vprof[%d]=%lf,d_prof[%d]=%lf\n",y,j,vel_profile[j],j,den_profile[j]);
    );

  for_parallel(i,is,domain.ie,
	for(j,domain.js,domain.je,
	  for(k,domain.ks,domain.ke,
		Region(i,j,k,  i,j,k,  INIT_TYPE3, vel_profile[j],0,0,P,den_profile[j]);
	  );
	);
  );


  );
);


Disc(
  NavierStokesPerfect(
    zetaA1=0.0e0;    {conditions the eigenvalue of the A jacobian}
    zetaA2=0.0e0;     {conditions eigenvalues so they don't go out of bound; set to 0.1-0.5}
  );
  MUSCL(
    FLUX=FLUX_FDS;
    AVERAGING=AVERAGING_ROE;
    AOWENO_TYPE=AOWENO_TYPE_DIFFUSIVE;
    AOWENO_gammalo=0.95;
    if (UNSTEADY,
      AOWENO_gammahi=0.9999;
    ,{else}
      AOWENO_gammahi=0.99;
    );
    INTERPOL=INTERPOL_AOWENO7; 
    EIGENVALCOND=EIGENVALCOND_PECLET;
    POSFILTER=POSFILTER_NONE;
    POSFILTER_numiter=4;
  );
  CDFplus(
    xi[1]=0.5;
    xi[2]=0.5;
    xi[3]=0.5;
    xi[4]=0.5;
    xi[5]=0.5;
  );
  Trapezoidal(
    weightm1_convection=0.5;
    weightm1_default=0.5;
  );
);


Cycle(
  NavierStokesPerfect(
    xiverge=1e-3;          {residual convergence threshold}
    rhoref=1.0;            {reference density in kg/m3}
    aref=300.0;            {reference sound speed in m/s}
    Uref[1]=rhoref;            
    Uref[2]=rhoref*aref;       
    Uref[3]=rhoref*aref;   
    Uref[4]=rhoref*aref;   
    Uref[5]=rhoref*aref*aref;  
  );
  SteadyState(
    if (mod(iter,100)==0,
      WriteDataFile(outputfilename);
    );
    sigma1=0.3;   {exponent of maximum pseudotime step in all dimensions}
    sigma2=0.3;   {exponent of maximum pseudotime step in all fluxes}
    if (iter==1,
      CFL=0.1;
    );
    CFL=max(CFL*0.999,0.1);
    UpdateFluid(CFL,PRECON_LOCALTIMESTEP,sigma1,sigma2);
    printf("%6.2f %6ld %9.3f   %E (%4ld,%4ld,%4ld)  %s\n",
           CFL,iter,effiter_U,ximax,i_ximax,
           j_ximax,k_ximax,clipinfo);
  );
  DualTimeStepping(
    tmax= 5e-2;    {simulation time span in seconds}
    dt=0.5*(1.5e-4/mf)/sqrt(1.4*286*Tinf);   {physical time step in seconds}
    sigma1=0.3;   {exponent of maximum pseudotime step in all dimensions}
    sigma2=0.3;   {exponent of maximum pseudotime step in all fluxes}
    
    iterskip=round(1.0/(dt*freq));
    if (mod(round(time/dt),iterskip)==0,
      HEATADDED="ON";
      if (iter==1,
        CFL=0.1;
        Model(NavierStokesPerfect(SetHeatDeposited(HEAT_is,HEAT_js,HEAT_ks, HEAT_ie,HEAT_je,HEAT_ke, iterskip*poweradded/(HEAT_xe-HEAT_xs)/(HEAT_ye-HEAT_ys)/(HEAT_ze-HEAT_zs) {W/m3});););
      );
      maxiter=75;
    ,{else}
      HEATADDED="OFF";
      if (iter==1,
        CFL=1.0;
        Model(NavierStokesPerfect(SetHeatDeposited(HEAT_is,HEAT_js,HEAT_ks, HEAT_ie,HEAT_je,HEAT_ke, 0.0 {W/m3});););
      );
      maxiter=15;
    );

    CFL=min(CFL*1.25,1.0);
    UpdateFluid(CFL,PRECON_LOCALTIMESTEP,sigma1,sigma2,dt);
    printf("%6.2f %6ld %9.3f   %E (%4ld,%4ld,%4ld)  %s  %s\n",
           CFL,iter,effiter_U,ximax,i_ximax,
           j_ximax,k_ximax,HEATADDED,clipinfo);
    if (ximax<xiverge || iter>maxiter,
      IncreaseTimeLevel();
      if (mod(round(time/dt),numtimelevelsskip_writedatafile)<1,
        WriteDataFile(outputfilename"."round(time/dt));
      );
      printf("dt=%E time=%Es\n",dt,time);
      if (time>tmax-0.1*dt,
        exit(EXIT_SUCCESS);
      );
    );
  );

);


Post(
  rhoinf=Pinf/Tinf/286;
  qinf=Minf*sqrt(1.4*286*Tinf);
  for (i,is+1,ie-1,
    Area[i]=_Area(i,1,ks+1, i,1,ke-1, 2,BDRY_WALLTFIXED1);
    Fshear[i]=_Fshear(i,1,ks+1, i,1,ke-1, 1,BDRY_WALLTFIXED1);
	avgCf[i]=Fshear[i]/Area[i]/(0.5*rhoinf*qinf^2);
	fprintf(postfilename,"%E %E\n",_x(i,1,1),avgCf[i]);
  );
  {
  POSTGRIDONLY=FALSE;
  WritePostFile(is,js,ks, ie,je,ke, "post.01", "tecplot", POSTGRIDONLY);
  }
);
